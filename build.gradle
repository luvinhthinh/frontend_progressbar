apply plugin: 'eclipse'
apply plugin: 'maven'
apply plugin: 'java'

repositories {
	mavenCentral()
}

configurations {
    cucumberRuntime {
        extendsFrom testRuntime
    }
}

dependencies {
	testCompile 'info.cukes:cucumber-junit:1.2.4',
			'info.cukes:cucumber-java:1.2.4',
			'org.seleniumhq.selenium:selenium-java:2.48.2',
			'org.seleniumhq.selenium:selenium-firefox-driver:2.48.2',
			'junit:junit:4.12'
}

sourceSets {
    test {
        java {
            srcDir 'test/features'
        }
        resources {
            srcDir 'test/features'
        }
    }
}

task cucumber() {
    dependsOn assemble, compileTestJava
    doLast {
        javaexec {
            main = "cucumber.api.cli.Main"
            classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.test.output
            args = ['--plugin', 'pretty', '--glue', 'gradle.cucumber', 'test/features']
        }
    }
}